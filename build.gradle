plugins {
    id 'org.springframework.boot' version '2.4.4'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'idea'
}

group = 'com.board'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}
sourceSets {
    integration {
        java.srcDir "$projectDir/src/integration_test/java"
        resources.srcDir "$projectDir/src/integration_test/resources"
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}
idea {
    module {
        testSourceDirs += sourceSets.integration.java.srcDirs
        testResourceDirs += sourceSets.integration.resources.srcDirs
    }
}
task integration(type: Test) {
    testClassesDirs = sourceSets.integration.output.classesDirs
    classpath = sourceSets.integration.runtimeClasspath
}
configurations {
    integrationImplementation.extendsFrom testImplementation
    integrationRuntime.extendsFrom testRuntime
}
check.dependsOn integration

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation "org.mapstruct:mapstruct:1.4.2.Final"
    implementation "org.projectlombok:lombok:1.18.16"
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'mysql:mysql-connector-java'
    annotationProcessor "org.mapstruct:mapstruct-processor:1.4.2.Final"
    annotationProcessor "org.projectlombok:lombok-mapstruct-binding:0.1.0"
    annotationProcessor "org.projectlombok:lombok:1.18.16"
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.mockito:mockito-junit-jupiter'
    integrationImplementation sourceSets.main.runtimeClasspath
    integrationImplementation sourceSets.test.runtimeClasspath
}

test {
    useJUnitPlatform()
}